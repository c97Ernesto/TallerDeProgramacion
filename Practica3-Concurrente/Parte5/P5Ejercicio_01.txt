programa P5Ejercicio_01

procesos

  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:= flores + 1
  fin
  proceso juntarPapeles(ES papeles: numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles:= papeles + 1
  fin
  proceso inicializarRobots
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, a1)
    EnviarMensaje(4, a2)
  fin


areas
  areaR1: AreaP(2, 1, 2, 100)
  areaR2: AreaP(3, 1, 3, 100)
  areaA1: AreaP(1, 98, 100, 98)
  areaA2: AreaP(1, 99, 100, 99)
  areaFiscalizador: AreaP(4, 1, 4, 1)

robots
  robot equipoRojo
  variables
    flores: numero
    id: numero

  comenzar
    flores:= 0
    RecibirMensaje(id, rFiscalizador)

    repetir 99
      juntarFlores(flores)
      si ((PosCa + 1 = 98) | ((PosCa + 1) = 99))
        BloquearEsquina(PosAv, PosCa + 1)
      mover
      si ((PosCa - 1 = 98) | ((PosCa - 1) = 99))
        LiberarEsquina(PosAv, PosCa - 1)

    juntarFlores(flores)
    EnviarMensaje(id, rFiscalizador)
    EnviarMensaje(flores, rFiscalizador)
  fin


  robot equipoAzul
  variables
    id: numero
    papeles: numero

  comenzar
    papeles:= 0
    RecibirMensaje(id, rFiscalizador)
    derecha

    repetir 99
      juntarPapeles(papeles)
      si (((PosAv + 1) = 2) | ((PosAv + 1) = 3))
        BloquearEsquina(PosAv + 1, PosCa)  
      mover
      si (((PosAv - 1) = 2) | ((PosAv - 1) = 3))
        LiberarEsquina(PosAv - 1, PosCa)

    juntarPapeles(papeles)
    EnviarMensaje(id, rFiscalizador)
    EnviarMensaje(papeles, rFiscalizador)
  fin


  robot fiscalizador
  variables
    id: numero
    puntos, puntosRojo, puntosAzul: numero

  comenzar
    puntosRojo:= 0
    puntosAzul:= 0
    inicializarRobots

    repetir 4
      RecibirMensaje(id, *)
      si (id = 1)
        RecibirMensaje(puntos, r1)
        puntosRojo:= puntosRojo + puntos
      sino
        si (id = 2)
          RecibirMensaje(puntos, r2)
          puntosRojo:= puntosRojo + puntos
        sino
          si (id = 3)
            RecibirMensaje(puntos, a1)
            puntosAzul:= puntosAzul + puntos
          sino
            RecibirMensaje(puntos, a2)
            puntosAzul:= puntosAzul + puntos

    si (puntosAzul > puntosRojo)
      Informar('Gano_equipo_azul_con ', puntosAzul)
    sino
      si (puntosAzul = puntosRojo)
        Informar('Empate_con ', puntosAzul)
      sino
        Informar('Gano_equipo_rojo_con ', puntosRojo)
  fin


variables
  r1: equipoRojo
  r2: equipoRojo
  a1: equipoAzul
  a2: equipoAzul
  rFiscalizador: fiscalizador
  
comenzar
  AsignarArea(r1, areaR1)
  AsignarArea(r2, areaR2)
  AsignarArea(a1, areaA1)
  AsignarArea(a2, areaA2)
  AsignarArea(rFiscalizador, areaFiscalizador)
  Iniciar(r1, 2, 1)
  Iniciar(r2, 3, 1)
  Iniciar(a1, 1, 98)
  Iniciar(a2, 1, 99)
  Iniciar(rFiscalizador, 4, 1)
fin
